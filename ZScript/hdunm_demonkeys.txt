class HDDemonKeyBase:HDPickup{
	string keytype;
	property keytype:keytype;
	string demonkeyname;
	property demonkeyname:demonkeyname;

	actor picktarget; //smh my head

	default{
		scale 0.6;
		radius 6;
		renderstyle "translucent";
		alpha 0.75;
		-hdpickup.fitsinbackpack
		+bright
		-hdpickup.droptranslation
		+inventory.invbar
		-hdpickup.notinpockets
		inventory.maxamount 1;
		inventory.interhubamount 1;
		hdpickup.bulk ENC_STIMPACK;
		tag "$TAG_DEMONKEY";
	}

	override void doeffect(){
		if(!keytype||!amount)destroy();
	}

	action void A_Zap(){
			lingeringthunderred.zap(self,invoker,invoker,15,true);
	}

	states{
	spawn:
		ART1 A 1; //GROSS HACK
		stop;

	use:
		---- A 0 A_JumpIfInventory(invoker.keytype,1,"incap");
		---- A 0{
		A_StartSound("brain/spit",CHAN_ITEM|CHANF_OVERLAP);
		A_GiveInventory(invoker.keytype);
		damagemobj(self,self,1,"balefire");
		A_GiveInventory("IsMoving",99);
		let hdp=HDPlayerPawn(self);
		if(hdp)
				hdp.usegametip("\cx"..HDDemonKeyBase.threat("keymsg",self));
		}
		#### ##### 1 A_SpawnItemEx("HDSmoke",0,0,0,random(4,0),random(-2,2),random(1,3),0,SXF_NOCHECKPOSITION);
		stop;

		incap:
		#### A 0 A_Zap();
		#### A 0{
			damagemobj(invoker,self,25,"balefire");
			for(int i=45;i<360;i+=90){
				A_SpawnItemEx("HDSmokeChunk",0,0,0,
					vel.x+frandom(-12,12),
					vel.y+random(-12,12),
					vel.z+frandom(4,16),
					0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM
				);
		}
			
		let hdp=HDPlayerPawn(self);
		if(hdp){
			hdp.incapacitated=1;
			hdp.incaptimer=700;}
		}
		#### A 0 A_DropItem(invoker.demonkeyname);
		stop;
	}

	const thcol="\cg";
	static string Threat(string filename,actor caller){
		string psss=Wads.ReadLump(Wads.CheckNumForName(filename,0));
		array<string> pss;pss.clear();
		psss.split(pss,"Threat ");
		pss.delete(0);
		string ps=pss[random(0,pss.size()-1)];
		ps=ps.mid(ps.indexof(" ")+1);
		ps=thcol..ps;
		int gender=caller.player.GetGender();
		if(gender==0){
			ps.replace("[pronouns]","HE loves");
			ps.replace("[altpronouns]","him");
			ps.replace("[ALTPRONOUNS2]","HIM");
		}else if(gender==1){
			ps.replace("[pronouns]","SHE loves");
			ps.replace("[altpronouns]","her");
			ps.replace("[ALTPRONOUNS2]","HER");
		}else if(gender==2){
			ps.replace("[pronouns]","THEY love");
			ps.replace("[altpronouns]","them");
			ps.replace("[ALTPRONOUNS2]","THEM");
		}else if(gender==3){
			ps.replace("[pronouns]","IT loves");
			ps.replace("[altpronouns]","it");
			ps.replace("[ALTPRONOUNS2]","IT");
		}
		ps.replace("/","\n"..thcol);
		ps.replace("|"," ");
		ps.replace("[User]",caller.getTag());
		ps.replace("  "," ");
		ps.replace("\n"..thcol.." ","\n"..thcol);
		return ps;
	}

}

class HDOrangeDemonKey:HDDemonKeyBase{
	default{
		//$Category "Items/Hideous Destructor/HDUnmaker"
		//$Title "Orange Demon Key"
		//$Sprite "ART1A0"
		inventory.icon "ART1A0";
		HDDemonKeyBase.KeyType "UnmakerUpgrade1Icon";
		inventory.pickupmessage "$PICKUP_GLUTTONY";
		HDDemonKeyBase.demonkeyname "HDOrangeDemonKey";
	}
	states{
	spawn:
		ART1 AABBCCDDEEDDCCBB 1 light("ROCKET"){A_SetTics(random(1,3)); if(!random(0,7))A_Zap();}
		loop;
	}
}

class HDPurpleDemonKey:HDDemonKeyBase{
	default{
		//$Category "Items/Hideous Destructor/HDUnmaker"
		//$Title "Purple Demon Key"
		//$Sprite "ART2A0"
		inventory.icon "ART2A0";
		HDDemonKeyBase.KeyType "UnmakerUpgrade2Icon";
		inventory.pickupmessage "$PICKUP_PRIDE";
		HDDemonKeyBase.demonkeyname "HDPurpleDemonKey";
	}
	states{
	spawn:
		ART2 AABBCCDDEEDDCCBB 1 light("PLAZBALL"){A_SetTics(random(1,6)); if(!random(0,7))A_Zap();}
		loop;

//unused code
	check:
		---- A 0 A_JumpIfInventory("UnmakerUpgrade1Icon",1,"givekey");
		goto incap;
	}
}
class HDCyanDemonKey:HDDemonKeyBase{
	default{
		//$Category "Items/Hideous Destructor/HDUnmaker"
		//$Title "Cyan Demon Key"
		//$Sprite "ART3A0"
		inventory.icon "ART3A0";

		HDDemonKeyBase.KeyType "UnmakerUpgrade3Icon";
		inventory.pickupmessage "$PICKUP_LUST";
		HDDemonKeyBase.demonkeyname "HDCyanDemonKey";
	}
	states{
	spawn:
		ART3 AABBCCDDEEDDCCBB 1 light("HEALTHPOTION"){A_SetTics(random(1,9)); if(!random(0,7))A_Zap();}
		loop;

//unused code. heh
//gee, you think that giving yourself UnmakerUpgrade2Icon and trying to activate the cyan demon key works?
	check:
		---- A 0 A_JumpIfInventory("UnmakerUpgrade1Icon",1,"check2");
		goto incap;

	check2:
		---- A 0 A_JumpIfInventory("UnmakerUpgrade2Icon",1,"givekey");
		goto incap;
	}
}

/*
THE DEMON KEYS

The Artifacts of Demonic origin. Each 3 "keys" represent
one of the three Seven Deadly Sins: Gluttony, Pride, and Lust.
According to reports of a classified division of UAC, trying
to use them while don't have the Unmaker or already having the "keys" will result in the items
sending anomalous effects to incapacitate a person almost
immediately.
Using them appropriately however, most, if not all people who absorbed them
heard rhetorics coming from voices of the non-existent, as if they were warning about something. They were known to
power up the unholy artifact.
*/